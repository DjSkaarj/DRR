Actor SentryGun 6200
{
	//$category Traps
	Obituary "$OB_SENTRY" 
	
	Health 800
	Radius 20
	Height 56
	Mass 0x7ffffff
	MaxTargetRange 2048
	Attacksound "sentry/attack"
  
	MONSTER
	+FLOORCLIP
	+NOBLOOD
	+NEVERRESPAWN
	+STANDSTILL
	+LOOKALLAROUND
	-COUNTKILL

	States
	{
	 Spawn:
		SENT A 4 A_Look
		loop
	 See:
		SENT A 1 A_Chase
		loop
	 Missile:
		SENT A 2 A_FaceTarget
		SENT B 1 BRIGHT A_CustomBulletAttack(15, 15, 1, 20, "BulletPuff", 0, CBAF_NORANDOM)
		SENT A 2 A_CposRefire
		goto Missile+1
	 Death:
		SENT C 1 A_PlaySound("Sentry/Death", 5)
		SENT C 0 A_PlaySound("Weapons/Rocklx", 6)
		//SENT C 0 A_SpawnItem("SentryBoom", 0, 48)
		SENT C 512
	 FadeOut:
		SENT C 2 A_FadeOut (0.1)
		loop
	}
}